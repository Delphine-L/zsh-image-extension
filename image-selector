# -*- mode: shell-script -*-
# vim: ft=zsh fdm=marker :

IMAGE_SELECTOR_SXIV=$HOME/.zsh-image-selector/sxiv
[ -x "$IMAGE_SELECTOR_SXIV" ] || IMAGE_SELECTOR_SXIV="${0:h}/sxiv"
[ -x "$IMAGE_SELECTOR_SXIV" ] || IMAGE_SELECTOR_SXIV="${0:A:h}/sxiv"

image-selector()
{
    autoload -U modify-current-argument
    autoload -U split-shell-arguments

    # local variables for split-shell-arguments
    local reply REPLY REPLY2

    local ARG
    local PREFIX

    ((--CURSOR))
    split-shell-arguments
    ((++CURSOR))

    ARG=$(eval echo $reply[$REPLY]) # evaluate tilde etc.

    # read the file prefix
    PREFIX=$PWD
    [ -n "$ARG" ] && PREFIX=${ARG%%/#}

    # if it is a directory, use its contents; also, corner case
    [ -d "$PREFIX" ] || [ "$ARG" = "/" ] && PREFIX=${PREFIX}/

    local FILES
    FILES=(${PREFIX}*(-.N))
    [ -z "$FILES" ] && return

    FILES=($($IMAGE_SELECTOR_SXIV -t -o $FILES 2> /dev/null | sort | uniq))
    [ -z "$FILES" ] && return

    if [ -n "$ARG" ]; then
        modify-current-argument '$FILES'
    else
        LBUFFER="${LBUFFER%% #} $FILES"
    fi
}
zle -N image-selector
bindkey ${IMAGE_SELECTOR_KEY:-'^X^I'} image-selector
